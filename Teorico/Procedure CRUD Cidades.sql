USE [sinqia]
GO

/****** Object:  StoredProcedure [dbo].[CRUD_Cidade]    Script Date: 14/03/2024 17:30:08 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[CRUD_Cidade]
	@CRUD VARCHAR(1),
    @CODIGO_CIDADE INT = NULL,			 
    @NOME VARCHAR(50) = NULL,			 
    @ESTADO VARCHAR(2) = NULL,			 
    @CEPINICIAL DECIMAL(18, 0) = NULL,	 
    @CEPFINAL DECIMAL(18, 0) = NULL,	 
    @STATUS VARCHAR(80) OUTPUT
AS
BEGIN
    SET NOCOUNT ON;
	IF (@CRUD = 'I')
	BEGIN
	    -- VERIFICO SE A CIDADE JÁ EXISTE
	    IF NOT EXISTS (SELECT 1 FROM CIDADES WHERE NOME = @NOME AND ESTADO = @ESTADO)
	    BEGIN
	        -- SE A CIDADE NÃO EXISTIR, REALIZO A INSERÇÃO
	        INSERT INTO CIDADES(
								NOME
							   ,ESTADO
							   ,CEP_INICIAL
							   ,CEP_FINAL
							   )
					VALUES	   (
							    @NOME 
							   ,@ESTADO
							   ,@CEPINICIAL
							   ,@CEPFINAL);
	
	        -- APÓS A INSERÇÃO, RETORNO O STATUS PARA TRATAMENTO NA APLICAÇÃO
	        SET @STATUS = 'CIDADE INSERIDA COM SUCESSO';
	    END
	    ELSE
	    BEGIN
	        -- SE A CIDADE JÁ EXISTIR, RETORNO O STATUS PARA TRATAMENTO NA APLICAÇÃO
	        SET @STATUS = 'CIDADE JÁ CADASTRADA NA BASE DE DADOS';
	    END;
	END
	ELSE
	IF (@CRUD = 'U')
	BEGIN
	    -- PRIMEIRO VERIFICO SE EXISTE UMA CIDADE COM O CODIGO FORNECIDO PARA PODER ALTERAR
		IF EXISTS (SELECT 1 FROM CIDADES WHERE CODIGO_CIDADE = @CODIGO_CIDADE)
		BEGIN
	    -- VERIFICO SE EXISTE APENAS UMA CIDADE NO BANCO COM AS INFORMAÇOES FORNECIDAS
			IF EXISTS (SELECT 1 FROM CIDADES WHERE NOME = @NOME AND ESTADO = @ESTADO AND CODIGO_CIDADE <> @CODIGO_CIDADE)
			BEGIN
				-- CASO EXISTA UMA CIDADE JÁ CADASTRADA NO BANCO, VERIFICO SE O CODIGO_CIDADE É DIFERENTE, SE FOR, PROIBO A ALTERAÇÃO
				-- PARA EVITAR A DUPLICAÇÃO DE DADOS
				-- RETORNO O STATUS PARA TRATAMENTO NA APLICAÇÃO
				RAISERROR('CIDADE E ESTADO JÁ CADASTRADOS NO SISTEMA', 16, 1)
				SET @STATUS = 'CIDADE E ESTADO JÁ CADASTRADOS NO SISTEMA';
				RETURN
			END
			ELSE
			BEGIN
	        -- POR FIM, REALIZO A ATUALIZAÇÃO DOS DADOS
					UPDATE [dbo].[CIDADES]
					   SET 
					       [NOME]		 = @NOME
					      ,[ESTADO]		 = @ESTADO
					      ,[CEP_INICIAL] = @CEPINICIAL
					      ,[CEP_FINAL]	 = @CEPFINAL
					 WHERE [dbo].[CIDADES].[CODIGO_CIDADE] = @CODIGO_CIDADE
	
			    -- APÓS O UPDATE, RETORNO O STATUS PARA TRATAMENTO NA APLICAÇÃO
			    SET @STATUS = 'CIDADE ATUALIZADA COM SUCESSO';
			END;
		END
		ELSE
		BEGIN
		    -- CASO A CIDADE NÃO SEJA LOCALIZADA, RETORNO STATUS PARA TRATAMENTO NA APLICAÇÃO
		    SET @STATUS = 'CIDADE NÃO LOCALIZADA PARA ALTUALIZAÇÃO';
	   	END;
	END
	IF (@CRUD = 'D')
	BEGIN
	    -- VERIFICO SE EXISTE ALGUM CLIENTE QUE TENHA A CIDADE CADASTRADA
		-- SE HOUVER, IMPEÇO A EXCLUSÃO
	    IF NOT EXISTS (SELECT 1 FROM CLIENTES WHERE CODIGO_CIDADE = @CODIGO_CIDADE)
		BEGIN
			BEGIN TRANSACTION;
			    DELETE FROM CIDADES WHERE CODIGO_CIDADE = @CODIGO_CIDADE;
			COMMIT TRANSACTION;
			-- APÓS DELETAR O REGISTRO, RETORNO O VALOR ZERO PARA TRATAMENTO NA APLICAÇÃO
		    SET @STATUS = 'CIDADE DELETADA COM SUCESSO';
		END
		ELSE
		BEGIN
		    -- SE HOUVER UM CLIENTE COM A CIDADE CADASTRADA, RETORNO O VALOR UM PARA TRATAMENTO NA APLICAÇÃO
		    SET @STATUS = 'NÃO É POSSÍVEL DELETAR CIDADE, HÁ CLIENTES RELACIONADOS A ESSE REGISTRO';
	   	END;
	END;
END;
GO

